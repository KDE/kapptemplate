#MIN_CONFIG

case $host in
*-*-linux* )
   AC_CHECK_HEADER(elf.h,
        shadow_support=yes,
        shadow_support=no
     )
     ;;
*-sun-solaris2.6)
    shadow_support=yes
    ;;
*-*-freebsd* )
   SETUIDFLAGS="4755 -o root"
     ;;
*-*-hpux* )
     ;;
esac

if test "$shadow_support" = "yes"; then
  AC_DEFINE(HAVE_SHADOW, 1, [Define if you use shadow passwords])
fi

if test "$SETUIDFLAGS" = ""; then
 if test "$shadow_support" = "no"; then
  SETUIDFLAGS=""
 else
  SETUIDFLAGS="-m 4755"
 fi
else
 SETUIDFLAGS="-m $SETUIDFLAGS"
fi
AC_SUBST(SETUIDFLAGS)

dnl Image readers/writers
KDE_CHECK_KIMGIO

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h crypt.h sys/select.h \
	sys/ioctl.h sys/stropts.h termio.h termios.h lastlog.h malloc.h \
	sys/sockio.h krb5/krb5.h rpc/rpc.h rpc/key_prot.h sys/m_wait.h \
	sys/param.h ncurses.h paths.h login_cap.h syslog.h ssl.h \
	sys/ucred.h sys/mount.h floatingpoint.h fstab.h mntent.h Alib.h)


AC_CHECK_LIB(rsaref, RSAPrivateEncrypt, [LIBSSL="-lssl -lcrypto -lRSAglue -lrsaref"],
	AC_CHECK_LIB(ssl, SSL_new, [LIBSSL="-lssl -lcrypto"]))
AC_SUBST(LIBSSL)

dnl Checks for Alib on HP-UX (added by deller, Apr 29 1999)
AC_CHECK_LIB(Alib, main, [LIBALIB="-lAlib"]) dnl HP-UX network audio server
AC_SUBST(LIBALIB)

dnl Autodetection now check for recent version features.
dnl If no libsmb is found on the system or if the version
dnl doesn't provide them, then a private version will
dnl be installed, only the lib and not the headers.
dnl This way there is no risk an external program can include
dnl our version by error (well, it can still link...),
dnl and we keep the benefit of a shared library.
AC_DEFUN(AC_FIND_SMB,
[
	AC_MSG_CHECKING([for libsmb])
	AC_CACHE_VAL(ac_cv_lib_smb,
	[
		kde_save_LIBS="$LIBS"
		LIBS="$LIBS $all_libraries -lsmb"
		kde_safe_cxxflags=$CXXFLAGS
		CXXFLAGS="$CXXFLAGS $all_includes"
		AC_LANG_SAVE
		AC_LANG_CPLUSPLUS
		AC_TRY_LINK(dnl check for a version of libsmb recent enough
		[#include<smb.h>
		class Toto : public SmbAnswerCallback
		{
		public:
			virtual char *getAnswer(const char *message, bool echo) {return 0;};
		} toto;
		],
		[SMBIO io(&toto);
		],
		ac_cv_lib_smb='-lsmb',
		ac_cv_lib_smb=no
		)
		AC_LANG_RESTORE
		CXXFLAGS=$kde_safe_cxxflags
		LIBS=$kde_save_LIBS
	]) dnl end of cache test
	if eval "test ! \"`echo $ac_cv_lib_smb`\" = no"; then
		AC_DEFINE_UNQUOTED(HAVE_LIBSMB, 1, [Define if you have libsmb])
	fi
	dnl If we need to compile prodived version of libsmb
	AM_CONDITIONAL(COMPILE_LIBSMB, test "$ac_cv_lib_smb" = no)
	LIBSMB="-lsmb"
	AC_SUBST(LIBSMB)
	AC_MSG_RESULT($ac_cv_lib_smb)
])
AC_FIND_SMB


AC_DEFUN(KDE_FIND_LDAP,
[
	AC_MSG_CHECKING([for libldap])
	AC_CACHE_VAL(ac_cv_lib_ldap,
	[
		kde_save_LIBS="$LIBS"
		LIBS="$LIBS $all_libraries -lldap -llber"
		kde_safe_cflags=$CFLAGS
		CFLAGS="$CFLAGS $all_includes"
		AC_LANG_SAVE
		AC_LANG_C
		AC_TRY_LINK(dnl
		[
		#include<lber.h>
		#include<ldap.h>
		],
		[
		LDAP *ldap;
		],
		ac_cv_lib_ldap='-lldap -llber',
		ac_cv_lib_ldap=no
		)
		AC_LANG_RESTORE
		CFLAGS=$kde_safe_cflags
		LIBS=$kde_save_LIBS
	]) dnl end of cache test
	LIBLDAP=""
	LDAP_SUBDIR=""
	if eval "test ! \"`echo $ac_cv_lib_ldap`\" = no"; then
		AC_DEFINE_UNQUOTED(HAVE_LIBLDAP, 1, [Define if you have libldap])
		LIBLDAP="$ac_cv_lib_ldap"
		LDAP_SUBDIR="ldap"
	fi
	AC_SUBST(LIBLDAP)
	AC_SUBST(LDAP_SUBDIR)
	AC_MSG_RESULT($ac_cv_lib_ldap)
])
KDE_FIND_LDAP


kde_have_setupterm=yes
AC_CHECK_LIB(curses, setupterm, [LIBCURSES=-lcurses],
  AC_CHECK_LIB(ncurses, setupterm, [LIBCURSES=-lncurses],
    AC_CHECK_LIB(mytinfo, setupterm, [LIBCURSES=-lmytinfo],
       kde_have_setupterm=no)))

AC_SUBST(LIBCURSES)

if test "$kde_have_setupterm" = "yes"; then
  AC_DEFINE_UNQUOTED(HAVE_SETUPTERM, 1, [Define if you have setupterm in -l(n)curses])
fi

kde_have_utempter=yes
AC_CHECK_LIB(utempter, addToUtmp, [LIBUTEMPTER=-lutempter], kde_have_utempter=no)
AC_SUBST(LIBUTEMPTER)
if test "$kde_have_utempter" = "yes"; then
  AC_DEFINE_UNQUOTED(HAVE_UTEMPTER, 1, [Define if you have the utempter helper for utmp managment])
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_LANG_CPLUSPLUS
AC_HEADER_TIME

dnl check if the compiler has bool
AC_CHECK_BOOL
AC_LANG_C
AC_C_LONG_DOUBLE
AC_TYPE_GETGROUPS

dnl for libsmb
AC_CHECK_SIZEOF(char,1)
AC_CHECK_SIZEOF(short,2)
AC_CHECK_SIZEOF(int,4)
AC_CHECK_SIZEOF(long,4)
AC_C_BIGENDIAN

AC_CHECK_FUNCS(socket powl sqrtl strdup getdtablesize vsnprintf setpgid nice seteuid snprintf vsyslog initgroups setgroups getgroups grantpt pw_encrypt setpriority getpt unlockpt ptsname)
AC_CHECK_SETENV
AC_CHECK_GETDOMAINNAME
AC_CHECK_GETHOSTNAME
AC_CHECK_USLEEP
AC_CHECK_RANDOM
AC_CHECK_S_ISSOCK
AC_CHECK_KDEMAXPATHLEN

AC_DEFUN(AC_CHECK_PW_EXPIRE,
[
	AC_MSG_CHECKING([for pw_expire])
	AC_CACHE_VAL(ac_cv_have_pw_expire,
	[
		AC_TRY_COMPILE(
		  [#include <pwd.h>],
		  [struct passwd pwd; pwd.pw_expire = 0;],
		  [ac_cv_have_pw_expire="yes"],
		  [ac_cv_have_pw_expire="no"]
		)
	]) dnl end AC_CACHE_VAL
	AC_MSG_RESULT($ac_cv_have_pw_expire)
	if test "$ac_cv_have_pw_expire" = "yes"; then
		AC_DEFINE(HAVE_PW_EXPIRE, 1, [Define if you have pw_expire])
	fi
])
AC_CHECK_PW_EXPIRE

dnl Perform program name transformation
AC_ARG_PROGRAM
 
AC_MSG_CHECKING([if it's safe to enable UTMP])

case "$host_os" in
linux*) utmp_support=yes;
     ;;
freebsd*) utmp_support=yes;
     ;;
*) utmp_support=no;
esac

AC_MSG_RESULT($utmp_support)

if test "$utmp_support" = yes; then
   AC_DEFINE_UNQUOTED(UTMP_SUPPORT, 1, [Define if you want to use utmp entries])
fi

AC_CHECK_UTMP_FILE

dnl Mico, mini STL, python checks
dnl KDE_CHECK_MICO()

dnl this is to let kappfinder get into the real AC_OUTPUT

